unit frmSudoku;

interface

uses
  Windows, Messages, SysUtils, Variants, Classes, Graphics, Controls, Forms,
  Dialogs, Grids, Gauges, StdCtrls, ExtCtrls, Menus, Solver;

const
  cNumberChar = ['0' .. '9'];

type
  TFormMain = class(TForm)
    Panel1: TPanel;
    Panel2: TPanel;
    bClear: TButton;
    bSave: TButton;
    bLoad: TButton;
    bPlay: TButton;
    Label1: TLabel;
    Label2: TLabel;
    Label3: TLabel;
    Label4: TLabel;
    Label5: TLabel;
    Label6: TLabel;
    Panel3: TPanel;
    lTime: TLabel;
    gridSudoku: TStringGrid;
    bCreate: TButton;
    Panel4: TPanel;
    Panel5: TPanel;
    Panel6: TPanel;
    Panel7: TPanel;
    Timer1: TTimer;
    OpenDialog1: TOpenDialog;
    SaveDialog1: TSaveDialog;
    Memo1: TMemo;
    Panel8: TPanel;
    bDigit1: TButton;
    bDigit2: TButton;
    bDigit3: TButton;
    bDigit4: TButton;
    bDigit5: TButton;
    bDigit6: TButton;
    bDigit7: TButton;
    bDigit8: TButton;
    bDigit9: TButton;
    bDelNumber: TButton;
    bSolve: TButton;
    procedure bClearClick(Sender: TObject);
    procedure FormCreate(Sender: TObject);
    procedure bPlayClick(Sender: TObject);
    procedure Timer1Timer(Sender: TObject);
    procedure bSaveClick(Sender: TObject);
    procedure bLoadClick(Sender: TObject);
    procedure btnNumberClick(Sender: TObject);
    procedure bDelNumberClick(Sender: TObject);
    procedure bCreateClick(Sender: TObject);
    procedure gridSudokuSelectCell(Sender: TObject; ACol, ARow: Integer; var CanSelect: Boolean);
    procedure gridSudokuDrawCell(Sender: TObject; ACol, ARow: Integer; Rect: TRect;
      State: TGridDrawState);
    procedure bSolveClick(Sender: TObject);
    procedure FormDestroy(Sender: TObject);
    procedure gridSudokuKeyDown(Sender: TObject; var Key: Word; Shift: TShiftState);
  private
    FSolver: TSudokuSolver;
    FCol, FRow: Integer;
    FCurrentDigit: string;
    FFile: TStringList;
    FButton: array [1 .. 9] of TButton;
    function GridToString(aLine: Boolean): string;
    procedure StringToGrid(aString: string);
    procedure CreateGrid(aMissing: Integer);
    procedure CheckWin;
    procedure UpdateButton;
    { Private declarations }
  public
    { Public declarations }
    ElapsedTime, Placed: Integer;
    Number: 1 .. 9;
    Jeu: Boolean;
    InitialDigit, UserDigit: array [0 .. 9, 0 .. 9] of char;
  end;

var
  FormMain: TFormMain;

implementation

{$R *.dfm}

procedure TFormMain.FormCreate(Sender: TObject);
var
  i: Integer;
begin
  Timer1.Enabled := False;
  Jeu := False;
  for i := 1 to 9 do
    FButton[i] := (FindComponent('bDigit' + IntToStr(i)) as TButton);
  FSolver := TSudokuSolver.Create;
  FFile := TStringList.Create;
  FCol := 0;
  FRow := 0;
end;

procedure TFormMain.FormDestroy(Sender: TObject);
begin
  FFile.Free;
  FSolver.Free;
end;

procedure TFormMain.UpdateButton();
begin
  Jeu := False;
  Timer1.Enabled := False;
  bClear.Enabled := True;
  bSave.Enabled := True;
  bLoad.Enabled := True;
  bPlay.Enabled := True;
  bCreate.Enabled := True;
end;

procedure TFormMain.CheckWin();
begin
  if Placed = 81 then
  begin
    Timer1.Enabled := False;
    ShowMessage('Tu as gagné !');
    UpdateButton();
  end;
end;

procedure TFormMain.bClearClick(Sender: TObject);
var
  i, j: Integer;
begin
  for i := 0 to 8 do
    for j := 0 to 8 do
    begin
      gridSudoku.Cells[i, j] := '';
      InitialDigit[j, i] := '-';
      UserDigit[i, j] := '0';
    end;
  UpdateButton();
end;

procedure TFormMain.bPlayClick(Sender: TObject);
var
  i, j: Integer;
begin
  Timer1.Enabled := True;
  bClear.Enabled := False;
  bSave.Enabled := True;
  bLoad.Enabled := False;
  bPlay.Enabled := False;
  bCreate.Enabled := False;
  ElapsedTime := 0;
  Jeu := True;
  Placed := 0;
  for i := 0 to 8 do
    for j := 0 to 8 do
    begin
      if gridSudoku.Cells[j, i] <> '' then
      begin
        InitialDigit[j, i] := 'x';
        UserDigit[j, i] := gridSudoku.Cells[j, i].Chars[0];
        Inc(Placed);
      end
      else
      begin
        InitialDigit[j, i] := '-';
        UserDigit[j, i] := #0;
      end;
    end;
  CheckWin();
end;

procedure TFormMain.Timer1Timer(Sender: TObject);
var
  sec, min: Integer;
begin
  Inc(ElapsedTime);
  min := ElapsedTime div 60;
  sec := ElapsedTime mod 60;
  if min = 0 then
    lTime.Caption := Format('Temps : %d s', [sec])
  else
    lTime.Caption := Format('Temps : %d min %d s', [min, sec]);
end;

function TFormMain.GridToString(aLine: Boolean): string;
var
  i, j: Integer;
begin
  result := '';
  for i := 0 to 8 do
  begin
    for j := 0 to 8 do
    begin
      if (gridSudoku.Cells[i, j] <> '') and CharInSet(gridSudoku.Cells[i, j].Chars[0], cNumberChar)
      then
        result := result + gridSudoku.Cells[i, j]
      else
        result := result + '0';
    end;
    if aLine then
      result := result + #13#10;
  end;
end;

procedure TFormMain.StringToGrid(aString: string);
var
  i, j: Integer;
begin
  if aString = '' then
    Exit;

  aString := StringReplace(aString, #13#10, '', [rfReplaceAll]);
  for i := 0 to 8 do
    for j := 0 to 8 do
    begin
      UserDigit[i, j] := aString[i * 9 + j + 1];
      if UserDigit[i, j] <> '0' then
        gridSudoku.Cells[i, j] := UserDigit[i, j]
      else
        gridSudoku.Cells[i, j] := '';
    end;
end;

procedure TFormMain.bLoadClick(Sender: TObject);
begin
  if OpenDialog1.Execute then
  begin
    FFile.LoadFromFile(OpenDialog1.FileName);
    StringToGrid(FFile.Text);
    if Memo1.Visible then
      Memo1.Text := FFile.Text;
  end;
end;

procedure TFormMain.bSaveClick(Sender: TObject);
begin
  if SaveDialog1.Execute then
  begin
    FFile.Text := GridToString(True);
    FFile.SaveToFile(SaveDialog1.FileName);
    if Memo1.Visible then
      Memo1.Text := FFile.Text;
  end;
end;

procedure TFormMain.bCreateClick(Sender: TObject);
var
  v, code: Integer;
  str: string;
begin
  str := '40';
  if InputQuery('Quelle valeur', 'Entrer le nombre de case manquante', str) then
  begin
    Val(str, v, code);
    if code = 0 then
      CreateGrid(v);
  end;
end;

procedure TFormMain.bSolveClick(Sender: TObject);
begin
  if (FSolver.Solve(GridToString(False))) then
    StringToGrid(FSolver.Solution);
  UpdateButton();
end;

procedure TFormMain.gridSudokuSelectCell(Sender: TObject; ACol, ARow: Integer;
  var CanSelect: Boolean);

  procedure CheckAlign(aVal: Integer; var min, max: Integer);
  begin
    if (aVal <= 2) then // 0, 1, 2
    begin
      min := 0;
      max := 2;
    end
    else if (aVal >= 6) then // 6, 7, 8
    begin
      min := 6;
      max := 8;
    end
    else // 3, 4, 5
    begin
      min := 3;
      max := 5;
    end;
  end;

var
  i, j: Integer;
  imin, imax, jmin, jmax: Integer;
  cDigit: string;
begin
  FCol := ACol;
  FRow := ARow;
  FCurrentDigit := gridSudoku.Cells[ACol, ARow];
  gridSudoku.Repaint;

  // Can not change if we play a game
  if (Jeu and (InitialDigit[ACol, ARow] = 'x')) then
  begin
    for i := 1 to 9 do
      FButton[i].Enabled := False;
    bDelNumber.Enabled := False;
    Exit;
  end;

  // Check butttons allowed
  for i := 1 to 9 do
    FButton[i].Enabled := True;
  bDelNumber.Enabled := True;

  for i := 0 to 8 do
    for j := 1 to 9 do
    begin
      cDigit := IntToStr(j);
      with gridSudoku do
      begin
        if (Cells[ACol, i] = cDigit) or (Cells[i, ARow] = cDigit) then
          FButton[j].Enabled := False;
      end;
    end;

  CheckAlign(ARow, imin, imax);
  CheckAlign(ACol, jmin, jmax);
  for i := imin to imax do
    for j := jmin to jmax do
    begin
      if gridSudoku.Cells[j, i] <> '' then
        FButton[StrToInt(gridSudoku.Cells[j, i].Chars[0])].Enabled := False;
    end;
end;

procedure TFormMain.btnNumberClick(Sender: TObject);
var
  n: char;
begin
  n := char(48 + (Sender as TButton).Tag);
  UserDigit[FCol, FRow] := n;
  FCurrentDigit := n;
  gridSudoku.Cells[FCol, FRow] := n;
  if Jeu then
  begin
    Inc(Placed);
    CheckWin();
  end;
  gridSudoku.Repaint;
  (Sender as TButton).Enabled := False;
end;

procedure TFormMain.bDelNumberClick(Sender: TObject);
begin
  gridSudoku.Cells[gridSudoku.Col, gridSudoku.Row] := '';
  UserDigit[gridSudoku.Col, gridSudoku.Row] := #0;
  if Jeu then
    Dec(Placed);
end;

procedure TFormMain.CreateGrid(aMissing: Integer);
var
  i: Integer;
  str: string;
  p: Integer;
begin
  str := '';
  for i := 1 to 81 do
    str := str + '0';

  p := random(81) + 1;
  str[p] := char(48 + random(9) + 1);

  FSolver.Solve(str);
  str := FSolver.Solution;
  i := 0;
  repeat
    p := random(81) + 1;
    if str[p] <> '0' then
    begin
      str[p] := '0';
      Inc(i);
    end;
  until (i = aMissing);

  StringToGrid(str);
end;

procedure TFormMain.gridSudokuDrawCell(Sender: TObject; ACol, ARow: Integer; Rect: TRect;
  State: TGridDrawState);
begin
  // if Jeu = true then
  with Sender as TStringGrid do
    with Canvas do
    begin
      if InitialDigit[ACol, ARow] = 'x' then
      begin
        Font.Color := clWhite;
        Brush.Color := clActiveCaption;
      end;

      if CharInSet(UserDigit[ACol, ARow], cNumberChar) then
      begin
        Font.Color := clBlack;
        Brush.Color := clWhite;
      end;

      if (FCol = ACol) or (FRow = ARow) then
        Brush.Color := RGB(226, 235, 243); // clCream;

      if (FCurrentDigit <> '') and (FCurrentDigit = gridSudoku.Cells[ACol, ARow]) then
        Brush.Color := RGB(187, 222, 251); // clWebLightCyan;

      if (FCol = ACol) and (FRow = ARow) then
        Brush.Color := RGB(187, 222, 251); // clWebLightCyan;

      FillRect(Rect);
      if UserDigit[ACol, ARow] = '0' then
        TextOut(Rect.Left + 20, Rect.Top + 8, '')
      else
        TextOut(Rect.Left + 20, Rect.Top + 8, UserDigit[ACol, ARow]);
    end;
end;

procedure TFormMain.gridSudokuKeyDown(Sender: TObject; var Key: Word; Shift: TShiftState);
begin
  // No modifier allowed
  if Shift <> [] then
  begin
    Exit;
  end;
  if not CharInSet(char(Key), cNumberChar) then
  Exit;
   btnNumberClick(FButton[Key - 48]);
end;

end.
